plugins {
	id 'com.google.cloud.tools.jib' version '2.7.1' apply false
	id 'org.springframework.boot' version '2.4.3' apply false
	id 'io.spring.dependency-management' version '1.0.11.RELEASE' apply false
	id 'java'
}

subprojects {
	apply plugin: 'java'
	apply plugin: 'org.springframework.boot'
	apply plugin: 'io.spring.dependency-management'
	apply plugin: 'com.google.cloud.tools.jib'

	group = 'example'
	version = '0.0.1-SNAPSHOT'
	sourceCompatibility = '11'

	repositories {
		mavenCentral()
	}

	ext {
		set('springCloudVersion', "2020.0.1")
	}

	dependencies {
		implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'
		testImplementation 'org.springframework.boot:spring-boot-starter-test'
	}

	dependencyManagement {
		imports {
			mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
		}
	}

	test {
		useJUnitPlatform()
	}

	wrapper {
		gradleVersion = "6.7"
	}
}